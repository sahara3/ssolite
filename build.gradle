buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        mavenCentral()
    }

    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.0.9.RELEASE'
        classpath 'com.github.spotbugs:spotbugs-gradle-plugin:3.0.0'
        classpath 'com.github.sahara3:gradle-tomcat-runner-plugin:0.2.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
    }
}

// ===================================================================
//  Common Settings
// ===================================================================
configure(allprojects) {
    repositories {
        mavenCentral()
    }

    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }

        dependencies {
            dependency 'org.projectlombok:lombok:1.18.10'

            dependency 'javax.servlet:javax.servlet-api:3.1.0'
            dependency 'javax.validation:validation-api:2.0.0.Final'
            dependency 'com.google.code.findbugs:jsr305:3.0.2'
            dependency 'org.dmfs:rfc3986-uri:0.8.1'

            dependency 'com.squareup.okhttp3:okhttp:3.14.7'

            dependency 'org.apache.struts:struts2-core:2.5.14.1'
        }
    }

    task configurations {
        group = 'help'
        description = "Displays all configurations declared in project '${project.path}'."
        doLast {
            println '-' * 60
            println "Project ${project.path}"
            println '-' * 60
            configurations.each { configuration ->
                println ''
                println "- ${configuration.name}"
                if (!configuration.extendsFrom.empty) {
                    println "  - extends from: ${configuration.extendsFrom.collect { it.name }.join(', ')}"
                }
            }
        }
    }
}

configure(subprojects) {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'com.github.spotbugs'

    ext {
        javaEncoding = 'UTF-8'
        eclipseFormatterXml = file("${rootDir}/eclipse/formatter.xml")
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(AbstractCompile) {
        options.encoding = javaEncoding
        options.compilerArgs += [
            '-Xlint:all', '-Xlint:-processing', '-Xlint:-fallthrough',
            '-parameters'
        ]
    }

    configurations {
        javadocLink {
            extendsFrom apiElements
        }
    }

    tasks.withType(Javadoc) {
        options.charSet = javaEncoding
        options.encoding = javaEncoding
        options.docEncoding = javaEncoding
        options.links 'https://docs.oracle.com/javase/8/docs/api/'
    }

    tasks.withType(Javadoc) { task ->
        ['Host', 'Port', 'User', 'Password'].each {
            def key = "proxy${it}"
            def value = System.properties.get("http.${key}")
            if (value) {
                task.options.jFlags "-D${key}=${value}"
            }
        }

        task.doFirst {
            def urlPrefix = 'https://javadoc.io/doc'

            configurations.javadocLink.resolvedConfiguration.resolvedArtifacts.findAll {
                !(it.id.componentIdentifier instanceof ProjectComponentIdentifier)
            }.collect {
                it.moduleVersion.id
            }.each {
                task.options.links "${urlPrefix}/${it.group}/${it.name}/${it.version}/"
            }

            configurations.javadocLink.resolvedConfiguration.resolvedArtifacts.findAll {
                it.id.componentIdentifier instanceof ProjectComponentIdentifier
            }.each {
                def m = it.moduleVersion.id
                def url = "${urlPrefix}/${m.group}/${m.name}/${m.version}/"
                def dir = project(it.id.componentIdentifier.projectPath).tasks.javadoc.destinationDir
                task.options.linksOffline url, dir.path
            }
        }
    }

    tasks.clean.doLast {
        project.delete 'bin'
    }

    eclipse {
        project {
            name = path.substring(1).replaceAll(':', '-')
        }

        classpath {
            downloadSources = true
            downloadJavadoc = true
        }

        jdt {
            file {
                withProperties { properties ->
                    def profiles = new XmlSlurper().parse(eclipseFormatterXml)
                    profiles.profile.setting.each { setting ->
                        properties.setProperty("${setting.@id}", "${setting.@value}")
                    }
                }
            }
        }
    }

    tasks.eclipse.doLast {
        def settingsDir = project.file('.settings')
        if (!settingsDir.exists()) {
            return
        }

        def update = { name, params ->
            def properties = new Properties()
            def settingFile = file("${settingsDir}/${name}")
            if (settingFile.exists()) {
                settingFile.withReader { r -> properties.load(r) }
            }
            else {
                properties.setProperty('eclipse.preferences.version', '1')
            }

            params.each { k, v ->
                properties.setProperty(k, v)
            }
            settingFile.withWriter { w -> properties.store(w, '') }
        }

        update('org.eclipse.core.runtime.prefs', [
            'line.separator': '\\n',
        ])
        update('org.eclipse.core.resources.prefs', [
            'encoding/<project>': javaEncoding,
        ])

        def profiles = new XmlSlurper().parse(eclipseFormatterXml)
        update('org.eclipse.jdt.ui.prefs', [
            'formatter_settings_version': "${profiles.profile.@version}",
            'org.eclipse.jdt.ui.ignorelowercasenames': 'true',
            'org.eclipse.jdt.ui.importorder': 'java;javax;org;com;',
            'org.eclipse.jdt.ui.ondemandthreshold': '99',
            'org.eclipse.jdt.ui.staticondemandthreshold': '99',
        ])
    }

    tasks.cleanEclipse.doLast {
        project.delete '.settings', '.classpath', '.project'
    }

    spotbugs {
        sourceSets = [sourceSets.main]
        ignoreFailures = false
        effort = 'max'
        reportLevel = 'low'
    }

    configurations {
        spotbugsLogging
    }

    dependencies {
        spotbugsLogging 'org.slf4j:slf4j-nop'
    }

    afterEvaluate {
        tasks.withType(com.github.spotbugs.SpotBugsTask) {
            reports.xml.enabled = false
            reports.html.enabled = true

            classpath += files(configurations.compileClasspath.resolvedConfiguration.files)
            spotbugsClasspath += files(configurations.spotbugsLogging.resolvedConfiguration.files)
        }
    }
}

// ===================================================================
//  ssolite
// ===================================================================
def mainProjects = subprojects.findAll { it.path.startsWith(':ssolite-') }

configure(mainProjects) {
    group = 'com.github.sahara3'
    version = '1.0.0'

    ext {
        isSnapshotVersion = version.endsWith('-SNAPSHOT')

        mavenPrivateUrl = properties.get('maven.private.url', null)
        mavenPrivateUsername = properties.get('maven.private.username', null)
        mavenPrivatePassword = properties.get('maven.private.password', null)

        bintrayUsername = properties.get('bintray.username', null)
        bintrayApiKey = properties.get('bintray.apikey', null)
    }

    task showVersion {
        doLast {
            println version
        }
    }

    apply plugin: 'java-library'
    apply plugin: 'jacoco'

    java {
        withJavadocJar()
        withSourcesJar()
    }

    configurations {
        providedApi
        api.extendsFrom providedApi

        optionalImplementation
        implementation.extendsFrom optionalImplementation
    }

    dependencies {
        providedApi 'javax.servlet:javax.servlet-api'
        compileOnly 'com.google.code.findbugs:jsr305'
        implementation 'org.slf4j:slf4j-api'

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'junit', module: 'junit'
        }
        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testImplementation 'org.junit.jupiter:junit-jupiter-params'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    }

    jar {
        manifest {
            attributes('Implementation-Title': name,
                       'Implementation-Version': archiveVersion)
        }
    }

    test {
        useJUnitPlatform()
        finalizedBy(jacocoTestReport)
    }

    jacocoTestReport {
        reports {
            xml.enabled false
            csv.enabled false
            html.enabled true
        }
    }

    // ---------------------------------------------------------------
    // Maven Publising
    // ---------------------------------------------------------------
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    publishing {
        publications {
            ssolite(MavenPublication) {
                from components.java

                versionMapping {
                    usage('java-api') {
                        fromResolutionResult()
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }

                pom {
                    name = project.name.split('-').collect {
                         (it == 'ssolite') ? 'SSOLite' : it.capitalize() }.join(' ')
                    url = 'https://github.com/sahara3/ssolite'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0'
                        }
                    }
                    developers {
                        developer {
                            id = 'sahara3'
                            name = 'Soichiro SAHARA'
                            email = 'soichiro.sahara@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/sahara3/ssolite.git'
                        developerConnection = 'scm:git:git://github.com/sahara3/ssolite.git'
                        url = 'https://github.com/sahara3/ssolite'
                    }

                    withXml {
                        // providedApi -> provided scope
                        asNode().dependencies.'*'.findAll {
                            project.configurations.providedApi.allDependencies.find { dep ->
                                dep.name == it.artifactId.text()
                            }
                        }.each {
                            it.scope*.value = 'provided'
                        }

                        // optionalImplementation -> optional=true
                        asNode().dependencies.'*'.findAll {
                            project.configurations.optionalImplementation.allDependencies.find { dep ->
                                    dep.name == it.artifactId.text()
                            }
                        }.each {
                            it.appendNode('optional').value = 'true'
                        }
                    }
                }
            }
        }
    }

    if (isSnapshotVersion) {
        if (mavenPrivateUrl != null) {
            publishing.repositories {
                maven {
                    url mavenPrivateUrl
                }
            }
            if (!mavenPrivateUrl.startsWith('file://')) {
                publishing.repositories.maven.credentials {
                    username mavenPrivateUsername
                    password mavenPrivatePassword
                }
            }
        }
    }

    afterEvaluate {
        publishing.publications.ssolite.pom.description = description

        bintray {
            user = bintrayUsername
            key = bintrayApiKey
            publications = ['ssolite']
            // dryRun = true

            pkg {
                repo = 'maven'
                name = project.name
                userOrg = 'ssolite'
                desc = project.description
                licenses = ['Apache-2.0']
                websiteUrl = publishing.publications.ssolite.pom.url.get()
                vcsUrl = "${publishing.publications.ssolite.pom.scm.url.get()}.git"
                issueTrackerUrl = "${publishing.publications.ssolite.pom.url.get()}/issues"
                publicDownloadNumbers = true
                version {
                    name = project.version
                }
            }
        }

        if (isSnapshotVersion) {
            tasks.bintrayUpload.enabled = false
            rootProject.tasks.bintrayUpload.enabled = false
            rootProject.tasks.bintrayPublish.enabled = false
        }
        else {
            tasks.publish.enabled = false
        }
    }
}

configure(project(':ssolite-core')) {
    description = 'A lightweight single sign on library (core module)'

    dependencies {
        implementation 'org.dmfs:rfc3986-uri'
        optionalImplementation 'com.squareup.okhttp3:okhttp'
        optionalImplementation 'com.fasterxml.jackson.core:jackson-databind'

        testImplementation 'org.springframework.security:spring-security-web'
    }
}

configure(project(':ssolite-spring')) {
    description = 'A lightweight single sign on library (for Spring)'

    dependencies {
        api project(':ssolite-core')
        api 'org.springframework.security:spring-security-core'
        api 'org.springframework.security:spring-security-web'
        api 'org.springframework.security:spring-security-config'
        api 'com.fasterxml.jackson.core:jackson-core'
        api 'com.fasterxml.jackson.core:jackson-databind'
        api 'com.fasterxml.jackson.core:jackson-annotations'
        implementation 'org.springframework:spring-core'
        implementation 'org.springframework:spring-web'
    }
}

configure(project(':ssolite-spring-boot-autoconfigure')) {
    description = 'A lightweight single sign on library (auto-configuration for Spring Boot)'

    dependencies {
        implementation project(':ssolite-core')
        implementation project(':ssolite-spring')
        implementation 'org.springframework:spring-core'
        implementation 'org.springframework:spring-context'
        implementation 'org.springframework.boot:spring-boot'
        implementation 'org.springframework.boot:spring-boot-autoconfigure'
        implementation 'org.springframework.security:spring-security-config'

        compileOnly 'org.springframework.boot:spring-boot-configuration-processor'
    }
}

configure(project(':ssolite-spring-boot-starter')) {
    description = 'A lightweight single sign on library (starter for Spring Boot)'

    dependencies {
        api project(':ssolite-core')
        api project(':ssolite-spring')
        api project(':ssolite-spring-boot-autoconfigure')
    }
}

// ===================================================================
//  Sample Server and Client
// ===================================================================
def sampleSpringProjects = subprojects.findAll {
    it.path.startsWith(':sample-') && it.path.endsWith('-spring')
}

def sampleStrutsProjects = subprojects.findAll {
    it.path.startsWith(':sample-') && it.path.endsWith('-struts2')
}

def allSampleProjects = sampleSpringProjects + sampleStrutsProjects

configure(allSampleProjects) {
    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        implementation 'javax.validation:validation-api'
    }

    tasks.withType(Javadoc) {
        enabled = false
    }
}

configure(sampleSpringProjects) {
    apply plugin: 'org.springframework.boot'

    dependencies {
        implementation project(':ssolite-spring-boot-starter')
        implementation 'javax.servlet:javax.servlet-api'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5'
        implementation 'org.slf4j:slf4j-api'
    }
}

configure(project(':sample-server-spring')) {
    dependencies {
        implementation 'org.springframework.session:spring-session-core'
    }
}

configure(project(':sample-client-spring')) {
    // nothing to add.
}

configure(sampleStrutsProjects) {
    apply plugin: 'war'
    apply plugin: 'com.github.sahara3.tomcat-runner'

    dependencies {
        compileOnly 'javax.servlet:javax.servlet-api'

        implementation project(':ssolite-core')
        implementation 'org.apache.struts:struts2-core'
        implementation 'org.slf4j:slf4j-api'
        runtimeOnly 'ch.qos.logback:logback-classic'
        runtimeOnly 'org.apache.logging.log4j:log4j-to-slf4j'
    }
}

configure(project(':sample-client-struts2')) {
    dependencies {
        implementation 'com.squareup.okhttp3:okhttp'
        implementation 'com.fasterxml.jackson.core:jackson-databind'
    }

    spotbugs {
        excludeFilter = file('spotbugs-exclude.xml')
    }

    // run this on tomcat.
    tomcat {
        version = 9.0
        port = 8081
        webapp(project) {
            contextPath = ''
        }
    }
}

buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        mavenCentral()
    }

    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.0.0.RELEASE'
        classpath 'org.akhikhl.gretty:gretty:2.0.0'
    }
}

// ===================================================================
//  Common Settings
// ===================================================================
allprojects {
    repositories {
        mavenCentral()
    }

    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
	}

        dependencies {
            dependency 'org.projectlombok:lombok:1.16.20'

            dependency 'javax.servlet:javax.servlet-api:3.1.0'
            dependency 'javax.validation:validation-api:2.0.0.Final'

            dependency 'com.squareup.okhttp3:okhttp:3.10.0'

            dependency 'org.apache.struts:struts2-core:2.5.14.1'
        }
    }
}

subprojects { subproject ->
    plugins.withId('java') {
        dependencies {
            compileOnly 'org.projectlombok:lombok'
            testCompileOnly 'org.projectlombok:lombok'
        }

        def encoding = 'UTF-8'

        sourceCompatibility = 1.8
        targetCompatibility = 1.8

        tasks.withType(AbstractCompile) {
            options.encoding = encoding
            options.compilerArgs += ['-Xlint:all', '-Xlint:-processing', '-Xlint:-fallthrough',
                                     '-parameters']
        }

        task delombok(type: JavaExec, dependsOn: compileJava) {
            ext.outputDir = file("${buildDir}/delombok")
            outputs.dir outputDir

            classpath configurations.compile, configurations.compileOnly
            main = 'lombok.launch.Main'
            args 'delombok'
            sourceSets.main.java.srcDirs.each {
                inputs.dir it
                args it, '-d', outputDir
            }
        }

        tasks.withType(Javadoc) {
            dependsOn delombok
            source = delombok.outputDir

            options.encoding = encoding
            options.charSet = encoding
            //options.links 'http://docs.oracle.com/javase/8/docs/api/'
        }
    }

    plugins.withId('eclipse') {
        eclipse {
            project {
                name = path.substring(1).replaceAll(':', '-')
                natures 'org.eclipse.buildship.core.gradleprojectnature'
                buildCommand 'org.eclipse.buildship.core.gradleprojectbuilder'
            }

            classpath {
                downloadSources = true
                downloadJavadoc = true

                file {
                    whenMerged { classpath ->
                        classpath.entries.findAll { it.kind == 'lib' }*.exported = true
                    }
                }
            }
        }

        tasks.eclipse {
            dependsOn cleanEclipse
            doLast {
                def settings_dir = file('.settings')
                if (settings_dir.exists()) {
                    def project_dir = (['..'] * project.depth).join('/')
                    file("${settings_dir}/org.eclipse.buildship.core.prefs").append("""
                        connection.gradle.distribution=GRADLE_DISTRIBUTION(WRAPPER)
                        connection.project.dir=${project_dir}
                        eclipse.preferences.version=1
                    """.stripIndent())
                }
            }
        }

        tasks.cleanEclipse {
            doLast {
                delete '.settings'
            }
        }

        tasks.clean {
            doLast {
                delete 'bin'
            }
        }
    }
}

// ===================================================================
//  ssolite
// ===================================================================
project(':ssolite') {
    group = 'com.github.sahara3'
    version = '0.0.3'

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'eclipse'

    dependencies {
        compileOnly 'javax.servlet:javax.servlet-api'
        compile 'javax.validation:validation-api'
        compile 'org.springframework:spring-web'
        compile 'org.springframework.security:spring-security-web'
        compile 'org.springframework.security:spring-security-config'
        compile 'org.springframework.boot:spring-boot'
        compile 'org.springframework.boot:spring-boot-autoconfigure'
        compile 'org.slf4j:slf4j-api'
    }

    jar {
        manifest {
            attributes('Implementation-Title': name,
                       'Implementation-Version': version)
        }
    }

    // ---------------------------------------------------------------
    // Maven Publising
    // ---------------------------------------------------------------
    configurations {
        deployerJars
    }

    dependencies {
        deployerJars 'org.apache.maven.wagon:wagon-ssh:2.2'
    }

    uploadArchives {
        repositories.mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: privateRepositoryUrl) {
                authentication(userName: privateRepositoryUsername,
                               password: privateRepositoryPassword)
            }
        }
    }

    task sourcesJar(type: Jar, dependsOn:classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn:javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}

// ===================================================================
//  Sample Server and Client
// ===================================================================
project(':sample-server') {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'org.springframework.boot'

    dependencies {
        compile project(':ssolite')
        compile 'javax.validation:validation-api'
        compile 'org.springframework.boot:spring-boot-starter-web'
        compile 'org.springframework.boot:spring-boot-starter-thymeleaf'
        compile 'org.springframework.boot:spring-boot-starter-security'
        compile 'org.springframework.session:spring-session-core'
        compile 'org.thymeleaf.extras:thymeleaf-extras-springsecurity4'
        compile 'org.slf4j:slf4j-api'
    }
}

project(':sample-client-spring') {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'org.springframework.boot'

    dependencies {
        compile project(':ssolite')
        compile 'javax.validation:validation-api'
        compile 'org.springframework.boot:spring-boot-starter-web'
        compile 'org.springframework.boot:spring-boot-starter-thymeleaf'
        compile 'org.springframework.boot:spring-boot-starter-security'
        compile 'org.thymeleaf.extras:thymeleaf-extras-springsecurity4'
        compile 'org.slf4j:slf4j-api'
    }
}

project(':sample-client-struts2') {
    apply plugin: 'java'
    apply plugin: 'war'
    apply plugin: 'eclipse'
    apply plugin: 'org.akhikhl.gretty'

    dependencies {
        compileOnly 'javax.servlet:javax.servlet-api'

        compile project(':ssolite')
        compile 'javax.validation:validation-api'
        compile 'org.apache.struts:struts2-core'
        compile 'com.squareup.okhttp3:okhttp'
        compile 'com.fasterxml.jackson.core:jackson-databind'
        compile 'org.slf4j:slf4j-api'
        runtime 'ch.qos.logback:logback-classic'
        runtime 'org.apache.logging.log4j:log4j-to-slf4j'
    }

    gretty {
        httpPort = 8081
        servletContainer = 'tomcat8'
        contextPath = '/'
    }

    afterEvaluate {
        configurations.runtime.resolvedConfiguration.resolvedArtifacts.each {
            gretty.classPath it.file
        }
    }
}
